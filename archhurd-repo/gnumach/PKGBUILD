# Maintainer: Etienne Brateau <etienne.brateau@gmail.com>

pkgbase=gnumach
pkgver=1.8
_commit=6fbf311687b8eb46f696f380f417157e4486d71d
pkgrel=1
pkgdesc="gnumach"
arch=(x86_64)
url="https://git.savannah.gnu.org/git/hurd/gnumach.git"
license=('GPL')
conflict=('linux')  # Just in case
depends=()
makedepends=(
  mig
  texinfo
)
options=(
  !debug
  !strip
)
source=(
  "git+https://git.savannah.gnu.org/git/hurd/gnumach.git#commit=$_commit"
)
sha256sums=('SKIP')
validpgpkeys=()


pkgver() {
  cd $pkgname
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  autoreconf -fi
}

build() {
  cd "$pkgbase"
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/libexec \
    --enable-kmsg \
    --enable-kdb \
    --disable-linux-groups &&
  make all
}

_package() {
  pkgdesc="The $pkgdesc kernel"

  cd "$pkgbase"
  make gnumach.gz
  mkdir -p "$pkgdir"/boot
  cp gnumach.gz "$pkgdir"/boot/
}

_package-headers() {
  pkgdesc="Headers for the $pkgdesc kernel"

  cd "$pkgbase"
  make DESTDIR="$pkgdir/" install-data
}

_package-docs() {
  pkgdesc="Documentation for the $pkgdesc kernel"

  cd "$pkgbase"
  make DESTDIR="$pkgdir/" install-man
}

pkgname=(
  "$pkgbase"
  "$pkgbase-headers"
  "$pkgbase-docs"
)
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim: set ts=8 sts=2 sw=2 et:
